Trace
---------------------------------------------------------------
    Defining frame__adventure_works__stores
---------------------------------------------------------------
;
Trace Setting variables...;
Let val__source_path = 'lib://DataFiles/Analytical Data Storage System/QVD/$(val__environment)/data_according_to_system/raw__adventure_works__stores.qvd';
Let val__target_path = '$(val__qvd_path__dab)/frame__adventure_works__stores.qvd';
Let val__source_create_time = Timestamp(FileTime('$(val__source_path)'), 'YYYY-MM-DD hh:mm:ss.fff');
Let val__target_create_time = Timestamp(FileTime('$(val__target_path)'), 'YYYY-MM-DD hh:mm:ss.fff');
Let val__source_is_newer = If('$(val__source_create_time)' > '$(val__target_create_time)', 1, 0);

Set var__hook__store = 'store__adventure_works|' & Text([business_entity_id]);
Set var__hook__person__sales = 'person__sales__adventure_works|' & Text([sales_person_id]);
Set var__record_version = Window(RecNo(), [business_entity_id], 'Asc', [record_loaded_at]);

Set var__valid_from = If(
        $(var__record_version) = 1,
        Timestamp#('1970-01-01 00:00:00.000', 'YYYY-MM-DD hh:mm:ss.fff'),
        [record_loaded_at]
    )
;

Set var__valid_to = Coalesce(
        Window([record_loaded_at], [business_entity_id], 'Asc', [record_loaded_at], 1, 1, 1),
        Timestamp#('9999-12-31 23:59:59.999', 'YYYY-MM-DD hh:mm:ss.fff')
    )
;

Set var__is_current_record = If(
        $(var__valid_to) = Timestamp#('9999-12-31 23:59:59.999', 'YYYY-MM-DD hh:mm:ss.fff'),
        1,
        0
    )
;

Set var__record_updated_at = If(
        $(var__is_current_record),
        [record_loaded_at],
        $(var__valid_to)
    )
;

Set var__pit_hook__store = $(var__hook__store) & '~epoch__valid_from|' & Text($(var__valid_from));

If $(val__source_is_newer) = 1 Then 

    Trace Source is newer, loading & transforming data...;
    [frame__adventure_works__stores]:
    Load
        Text($(var__pit_hook__store)) As [_pit_hook__store],
        Text($(var__hook__store)) As [_hook__store],
        Text($(var__hook__person__sales)) As [_hook__person__sales],
        Num#([business_entity_id]) As [store__business_entity_id],
        Text([name]) As [store__name],
        Num#([sales_person_id]) As [store__sales_person_id],
        Text([demographics]) As [store__demographics],
        Text([rowguid]) As [store__rowguid],
        Date(Num#([modified_date]), 'YYYY-MM-DD') As [store__modified_date],
        Text([record_hash]) As [store__record_hash],
        Timestamp([record_loaded_at], 'YYYY-MM-DD hh:mm:ss.fff') As [store__record_loaded_at],
        Timestamp($(var__record_updated_at), 'YYYY-MM-DD hh:mm:ss.fff') As [store__record_updated_at],
        Num($(var__record_version)) As [store__record_version],
        Timestamp($(var__valid_from), 'YYYY-MM-DD hh:mm:ss.fff') As [store__record_valid_from],
        Timestamp($(var__valid_to), 'YYYY-MM-DD hh:mm:ss.fff') As [store__record_valid_to],
        Num($(var__is_current_record)) As [store__is_current_record]

    From
        [lib://DataFiles/Analytical Data Storage System/QVD/$(val__environment)/data_according_to_system/raw__adventure_works__stores.qvd] (qvd)
    ;

    Trace Commenting table...;
    Comment Table [frame__adventure_works__stores] With 'Customers (resellers) of Adventure Works products.';

    Trace Commenting fields...;
    Comment Field [_pit_hook__store] With 'Point in time version of _hook__store.';
    Comment Field [_hook__store] With 'Hook for business_entity_id using keyset: store__adventure_works.';
    Comment Field [_hook__person__sales] With 'Hook for sales_person_id using keyset: person__sales__adventure_works.';
    Comment Field [store__business_entity_id] With 'Primary key. Foreign key to Customer.BusinessEntityID.';
    Comment Field [store__name] With 'Name of the store.';
    Comment Field [store__sales_person_id] With 'ID of the sales person assigned to the customer. Foreign key to SalesPerson.BusinessEntityID.';
    Comment Field [store__demographics] With 'Demographic information about the store such as the number of employees, annual sales and store type.';
    Comment Field [store__rowguid] With 'ROWGUIDCOL number uniquely identifying the record. Used to support a merge replication sample.';
    Comment Field [store__modified_date] With 'Date and time the record was last updated.';
    Comment Field [store__record_hash] With 'Hash of the record.';
    Comment Field [store__record_loaded_at] With 'Date and time the record was loaded.';
    Comment Field [store__record_updated_at] With 'Date and time the record was last updated.';
    Comment Field [store__record_version] With 'Version of the record.';
    Comment Field [store__record_valid_from] With 'Date and time the record was valid from.';
    Comment Field [store__record_valid_to] With 'Date and time the record was valid to.';
    Comment Field [store__is_current_record] With '1 if the record is current, 0 otherwise.';

    Trace Storing data...;
    Store [frame__adventure_works__stores] Into [$(val__qvd_path__dab)/frame__adventure_works__stores.qvd] (qvd);

    Trace Dropping table...;
    Drop Table [frame__adventure_works__stores];

Else
    Trace Source is older than target, skipping...;

End If

Trace Resetting variables...;
Let val__source_path = Null();
Let val__target_path = Null();
Let val__source_create_time = Null();
Let val__target_create_time = Null();
Let val__source_is_newer = Null();

Let var__record_version = Null();
Let var__valid_from = Null();
Let var__valid_to = Null();
Let var__is_current_record = Null();
Let var__record_updated_at = Null();
Let var__hook__store = Null();
Let var__hook__person__sales = Null();
Let var__pit_hook__store = Null();
