Trace
---------------------------------------------------------------
    Extracting raw__adventure_works__sales_persons
---------------------------------------------------------------
;
Trace Setting variables...;
Let val__source_path = 'lib://OneDrive - mattias.thalen@two.se/Qlik/Analytical Data Storage System/data/das.raw__adventure_works__sales_persons.parquet';
Let val__target_path = '$(val__qvd_path__das)/raw__adventure_works__sales_persons.qvd';
Let val__source_create_time = Timestamp(FileTime('$(val__source_path)'), 'YYYY-MM-DD hh:mm:ss.fff');
Let val__target_create_time = Timestamp(FileTime('$(val__target_path)'), 'YYYY-MM-DD hh:mm:ss.fff');
Let val__target_exists = If(Len('$(val__target_create_time)') > 0, 1, 0);
Let val__source_is_newer = If('$(val__source_create_time)' > '$(val__target_create_time)', 1, 0);
Let val__incremental_value = '1970-01-01';

If $(val__source_is_newer) = 1 Then
    Trace Source is newer, loading & transforming data...;

    Trace Define hash table...;
    [processed_record_hashes]:
    Load
        Null() As [old_record_hash]
    AutoGenerate 0
    ;

    Trace Checking if target QVD exists...;
    If $(val__target_exists) = 1 Then
        Trace Target found, loading hashes and max incremental value...;

        Concatenate([processed_record_hashes])
        Load
            [record_hash] As [old_record_hash]

        From
            [$(val__target_path)] (qvd)
        ;

        [max_incremental_value]:
        Load
            Date(Max(Num#([modified_date])), 'YYYY-MM-DD') As [max_incremental_value]
        From
            [$(val__target_path)] (qvd)
        ;

        Let val__incremental_value = Coalesce(Peek('max_incremental_value', -1, 'max_incremental_value'), '$(val__incremental_value)');
        Drop Table [max_incremental_value];

    Else
        Trace Target not found, starting full load...;

    End If

    Trace Loading new data with incremental value $(val__incremental_value)...;
    Set var__record_hash = Hash256(
        [business_entity_id],
        [territory_id],
        [bonus],
        [commission_pct],
        [sales_last_year],
        [sales_quota],
        [sales_ytd],
        [modified_date],
        [rowguid]
    )
    ;

    [raw__adventure_works__sales_persons]:
    Load
        Text([business_entity_id]) As [business_entity_id],
        Text([territory_id]) As [territory_id],
        Text([bonus]) As [bonus],
        Text([commission_pct]) As [commission_pct],
        Text([sales_last_year]) As [sales_last_year],
        Text([sales_quota]) As [sales_quota],
        Text([sales_ytd]) As [sales_ytd],
        Text([modified_date]) As [modified_date],
        Text([rowguid]) As [rowguid],
        $(var__record_hash) As [record_hash],
        Timestamp#('$(val__utc)', 'YYYY-MM-DD hh:mm:ss.fff') As [record_loaded_at]

    From
        [$(val__source_path)] (parquet)

    Where
        1 = 1
        And Date([modified_date], 'YYYY-MM-DD') >= Date#('$(val__incremental_value)', 'YYYY-MM-DD')
        And Not Exists ([old_record_hash], $(var__record_hash))
    ;

    Trace Dropping hash table...;
    Drop Table [processed_record_hashes];

    Trace Counting new records...;
    Set val__no_of_new_records = Alt(NoOfRows('raw__adventure_works__sales_persons'), 0);

    Trace Checking if there are new records...;
    If $(val__no_of_new_records) > 0 Then

        Trace Checking if target QVD exists...;
        If $(val__target_exists) = 1 Then
            Trace Appending previously ingested data...;

            Concatenate([raw__adventure_works__sales_persons])
            Load * From [$(val__target_path)] (qvd) Where Not Exists ([record_hash]);

        Else
            Trace Target not found, skipping append...;

        End If

        Trace Commenting table...;
        Comment Table [raw__adventure_works__sales_persons] With 'Sales representative current information.';

        Trace Commenting fields...;
        Comment Field [business_entity_id] With 'Primary key for SalesPerson records. Foreign key to Employee.BusinessEntityID.';
        Comment Field [territory_id] With 'Territory currently assigned to. Foreign key to SalesTerritory.SalesTerritoryID.';
        Comment Field [bonus] With 'Bonus due if quota is met.';
        Comment Field [commission_pct] With 'Commission percent received per sale.';
        Comment Field [sales_last_year] With 'Sales total of previous year.';
        Comment Field [sales_quota] With 'Projected yearly sales.';
        Comment Field [sales_ytd] With 'Sales total year to date.';
        Comment Field [modified_date] With 'Date and time the record was last updated.';
        Comment Field [rowguid] With 'ROWGUIDCOL number uniquely identifying the record. Used to support a merge replication sample.';
        Comment Field [record_hash] With 'Hash of the record, used for deduplication.';
        Comment Field [record_loaded_at] With 'Timestamp when the record was loaded.';

        Trace Storing data...;
        Store [raw__adventure_works__sales_persons] Into [$(val__qvd_path__das)/raw__adventure_works__sales_persons.qvd] (qvd);

    Else
        Trace No new records loaded...;

    End If

    Trace Dropping table...;
    Drop Table [raw__adventure_works__sales_persons];

Else
    Trace Source is older than target, skipping...;

End If

Trace Resetting variables...;
Let val__source_path = Null();
Let val__target_path = Null();
Let var__source_create_time = Null();
Let var__target_create_time = Null();
Let val__target_exists = Null();
Let val__source_is_newer = Null();
Let val__incremental_value = Null();
Let var__record_hash = Null();
Let val__no_of_new_records = Null();
