Trace
---------------------------------------------------------------
    Extracting raw__adventure_works__sales_order_details
---------------------------------------------------------------
;
Trace Setting variables...;
Let val__source_path = 'lib://OneDrive - mattias.thalen@two.se/Qlik/Analytical Data Storage System/data/das.raw__adventure_works__sales_order_details.parquet';
Let val__target_path = '$(val__qvd_path__das)/raw__adventure_works__sales_order_details.qvd';
Let val__source_create_time = Timestamp(FileTime('$(val__source_path)'), 'YYYY-MM-DD hh:mm:ss.fff');
Let val__target_create_time = Timestamp(FileTime('$(val__target_path)'), 'YYYY-MM-DD hh:mm:ss.fff');
Let val__target_exists = If(Len('$(val__target_create_time)') > 0, 1, 0);
Let val__source_is_newer = If('$(val__source_create_time)' > '$(val__target_create_time)', 1, 0);
Let val__incremental_value = '1970-01-01';

If $(val__source_is_newer) = 1 Then
    Trace Source is newer, loading & transforming data...;

    Trace Define hash table...;
    [processed_record_hashes]:
    Load
        Null() As [old_record_hash]
    AutoGenerate 0
    ;

    Trace Checking if target QVD exists...;
    If $(val__target_exists) = 1 Then
        Trace Target found, loading hashes and max incremental value...;

        Concatenate([processed_record_hashes])
        Load
            [record_hash] As [old_record_hash]

        From
            [$(val__target_path)] (qvd)
        ;

        [max_incremental_value]:
        Load
            Date(Max(Num#([modified_date])), 'YYYY-MM-DD') As [max_incremental_value]
        From
            [$(val__target_path)] (qvd)
        ;

        Let val__incremental_value = Coalesce(Peek('max_incremental_value', -1, 'max_incremental_value'), '$(val__incremental_value)');
        Drop Table [max_incremental_value];

    Else
        Trace Target not found, starting full load...;

    End If

    Trace Loading new data with incremental value $(val__incremental_value)...;
    Set var__record_hash = Hash256(
        [product_id],
        [sales_order_detail_id],
        [sales_order_id],
        [special_offer_id],
        [carrier_tracking_number],
        [line_total],
        [order_qty],
        [unit_price],
        [unit_price_discount],
        [modified_date],
        [rowguid]
    )
    ;

    [raw__adventure_works__sales_order_details]:
    Load
        Text([product_id]) As [product_id],
        Text([sales_order_detail_id]) As [sales_order_detail_id],
        Text([sales_order_id]) As [sales_order_id],
        Text([special_offer_id]) As [special_offer_id],
        Text([carrier_tracking_number]) As [carrier_tracking_number],
        Text([line_total]) As [line_total],
        Text([order_qty]) As [order_qty],
        Text([unit_price]) As [unit_price],
        Text([unit_price_discount]) As [unit_price_discount],
        Text([modified_date]) As [modified_date],
        Text([rowguid]) As [rowguid],
        $(var__record_hash) As [record_hash],
        Timestamp#('$(val__utc)', 'YYYY-MM-DD hh:mm:ss.fff') As [record_loaded_at]

    From
        [$(val__source_path)] (parquet)

    Where
        1 = 1
        And Date([modified_date], 'YYYY-MM-DD') >= Date#('$(val__incremental_value)', 'YYYY-MM-DD')
        And Not Exists ([old_record_hash], $(var__record_hash))
    ;

    Trace Dropping hash table...;
    Drop Table [processed_record_hashes];

    Trace Counting new records...;
    Set val__no_of_new_records = Alt(NoOfRows('raw__adventure_works__sales_order_details'), 0);

    Trace Checking if there are new records...;
    If $(val__no_of_new_records) > 0 Then

        Trace Checking if target QVD exists...;
        If $(val__target_exists) = 1 Then
            Trace Appending previously ingested data...;

            Concatenate([raw__adventure_works__sales_order_details])
            Load * From [$(val__target_path)] (qvd) Where Not Exists ([record_hash]);

        Else
            Trace Target not found, skipping append...;

        End If

        Trace Commenting table...;
        Comment Table [raw__adventure_works__sales_order_details] With 'Individual products associated with a specific sales order. See SalesOrderHeader.';

        Trace Commenting fields...;
        Comment Field [product_id] With 'Product sold to customer. Foreign key to Product.ProductID.';
        Comment Field [sales_order_detail_id] With 'Primary key. One incremental unique number per product sold.';
        Comment Field [sales_order_id] With 'Primary key. Foreign key to SalesOrderHeader.SalesOrderID.';
        Comment Field [special_offer_id] With 'Promotional code. Foreign key to SpecialOffer.SpecialOfferID.';
        Comment Field [carrier_tracking_number] With 'Shipment tracking number supplied by the shipper.';
        Comment Field [line_total] With 'Per product subtotal. Computed as UnitPrice * (1 - UnitPriceDiscount) * OrderQty.';
        Comment Field [order_qty] With 'Quantity ordered per product.';
        Comment Field [unit_price] With 'Selling price of a single product.';
        Comment Field [unit_price_discount] With 'Discount amount.';
        Comment Field [modified_date] With 'Date and time the record was last updated.';
        Comment Field [rowguid] With 'ROWGUIDCOL number uniquely identifying the record. Used to support a merge replication sample.';
        Comment Field [record_hash] With 'Hash of the record, used for deduplication.';
        Comment Field [record_loaded_at] With 'Timestamp when the record was loaded.';

        Trace Storing data...;
        Store [raw__adventure_works__sales_order_details] Into [$(val__qvd_path__das)/raw__adventure_works__sales_order_details.qvd] (qvd);

    Else
        Trace No new records loaded...;

    End If

    Trace Dropping table...;
    Drop Table [raw__adventure_works__sales_order_details];

Else
    Trace Source is older than target, skipping...;

End If

Trace Resetting variables...;
Let val__source_path = Null();
Let val__target_path = Null();
Let var__source_create_time = Null();
Let var__target_create_time = Null();
Let val__target_exists = Null();
Let val__source_is_newer = Null();
Let val__incremental_value = Null();
Let var__record_hash = Null();
Let val__no_of_new_records = Null();
